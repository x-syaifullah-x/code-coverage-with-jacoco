apply plugin: "jacoco"

jacoco {
    reportsDir = file("$buildDir/${project.name}_code_coverage")
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

project.afterEvaluate {
    android.'applicationVariants'.all { variant ->
        def variantName = variant.name
        def testVariantNameCodeCoverageTask = "${variantName.toLowerCase()}CodeCoverage"

        def appUnitTest = "test${variantName.capitalize()}UnitTest"
        def FavoriteUnitTest = ":favorite:test${variantName.capitalize()}UnitTest"

        tasks.create(
                name: "${testVariantNameCodeCoverageTask}",
                type: JacocoReport,
                dependsOn: [FavoriteUnitTest, appUnitTest]
        ) {
            group = "Reporting"
            description = "Generate Jacoco coverage reports for the ${variantName.capitalize()} build"

            reports {
                html.enabled = true
                xml.enabled = false
                csv.enabled = false
            }

            def fileFilter = [
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*Test*.*',
                    '**/com/example/databinding/*',
                    '**/com/example/generated/callback/*',
                    '**/android/databinding/*',
                    '**/androidx/databinding/*',
                    '**/di/module/*',
                    '**/*MapperImpl*.*',
                    '**/*$ViewInjector*.*',
                    '**/*$ViewBinder*.*',
                    '**/BuildConfig.*',
                    '**/*Component*.*',
                    '**/*BR*.*',
                    '**/Manifest*.*',
                    '**/*$Lambda$*.*',
                    '**/*Companion*.*',
                    '**/*Module.*', /* filtering Dagger modules classes */
                    '**/*Dagger*.*',/* filtering Dagger-generated classes */
                    '**/*MembersInjector*.*',
                    '**/*_Factory*.*',
                    '**/*_Provide*Factory*.*',
                    '**/*Extensions*.*',
                    '**/*$Result.*', /* filtering `sealed` and `data` classes */
                    '**/*$Result$*.*',/* filtering `sealed` and `data` classes */
                    '**/*Args*.*', /* filtering Navigation Component generated classes */
                    '**/*Directions*.*' /* filtering Navigation Component generated classes */
            ]

            classDirectories.setFrom(files([
                    fileTree(dir: "$rootDir/app/build/tmp/kotlin-classes/$variantName", excludes: fileFilter),
                    fileTree(dir: "$rootDir/favorite/build/tmp/kotlin-classes/$variantName", excludes: fileFilter),
                    fileTree(dir: "$rootDir/core/build/tmp/kotlin-classes/$variantName", excludes: fileFilter),
            ]))

            def coverageSource = files(
                    "$rootDir/app/src/main/java",
                    "$rootDir/favorite/src/main/java",
                    "$rootDir/core/src/main/java",
            )

            additionalSourceDirs.setFrom(coverageSource)
            sourceDirectories.setFrom(coverageSource)

            executionData(files([
                    "$rootDir/app/build/jacoco/${appUnitTest}.exec",
                    "$rootDir/favorite/build/jacoco/${appUnitTest}.exec",
                    "$rootDir/core/build/jacoco/${appUnitTest}.exec",
            ]))
        }
    }
}
